! Alphabet
define Vowel ए | ऐ | उ | ऊ | इ | ई | ओ | औ | आ | अ | ऋ | ॠ | ऌ | ॡ;
define Matra े | ै | ु | ू | ि | ी | ो | ौ | ा | ॉ | ृ | ॄ | ॢ | ॣ;
define Nasalize ं | ँ;
define Consonant व | र | त | थ | य | ञ | प | फ | स | ष | द | ध | ट | ठ | ग | घ | ह | ज | झ | क | ख | ल | श | च | छ | ड | ढ | ब | भ | न | ण | म | ़;
define Visarga ः;
define Halant ्;
define Nuqta ़;
define Abbrev ॰;
define Word [Vowel | Consonant] [Vowel | Consonant | Halant | Nuqta | Nasalize | Matra | Visarga]* [Abbrev | 0];
define Char [Vowel | Consonant | Halant | Nuqta | Nasalize | Matra | Visarga];

! Pronouns
read lexc pronouns.lexc
define Pronoun

! Verbs
define GuessVerb Char* [Char-ै-ु-ि-ौ] [0 | ँ | ं] ["+Guess":0];
read lexc verbs.lexc
substitute defined GuessVerb for "^GuessVerb"
define Verb
define VerbVowelWeakening ी -> ि, ई -> इ || _ "^" [आ | ए | {ऊँ} | ओ] ,,
       			  ू -> ु, ऊ -> उ || _ "^" [ए | {ऊँ} | ओ] ,,
       			  ी -> 0, ई -> 0, ि -> 0, इ -> 0 || _ "^" [ी | ई] ;
define VerbYEpenthesis [..] -> य || [Vowel-ई | Matra-ी] _ "^" आ ,,
       		       [..] -> य || [ए | े] _ "^" [ए | े] ;

! Adjectives
define GuessAdjectiveI Char+ [? - ा] ["+Guess":0];
define GuessAdjectiveII Char+ ा ["+Guess":0];
read lexc newadjectives.lexc
substitute defined GuessAdjectiveI for "^GuessAdjectiveI"
substitute defined GuessAdjectiveII for "^GuessAdjectiveII"
define Adjective

define AdjADeletion ा -> 0 || _ "^" [ी | े];

! Nouns
define GuessNounI Char+ ा ["+Guess":0];
define GuessNounII Char+ [? - ा] ["+Guess":0];
define GuessNounIII Char+ [ी | ि | {िया}] ["+Guess":0];
define GuessNounIV Char+ [? - [ी | ि | {िया}]] ["+Guess":0];
read lexc newnouns.lexc
substitute defined GuessNounI for "^GuessNounI"
substitute defined GuessNounII for "^GuessNounII"
substitute defined GuessNounIII for "^GuessNounIII"
substitute defined GuessNounIV for "^GuessNounIV"
define Noun
define NounADeletion ा -> 0 || _ "^" [ो ं | े];
define NounIDeletion ी -> 0, ि -> 0, {िया} -> 0 || _ "^" [{ियाँ} | {ियों}];

define VowelToMatra ए -> े || Consonant "^" _ ,,
       		    ऐ -> ै || Consonant "^" _ ,,
		    उ -> ु || Consonant "^" _ ,,
		    ऊ -> ू || Consonant "^" _ ,,
 		    इ -> ि || Consonant "^" _ ,,
		    ई -> ी || Consonant "^" _ ,,
		    ओ -> ो || Consonant "^" _ ,,
		    औ -> ौ || Consonant "^" _ ,,
		    आ -> ा || Consonant "^" _ ,,
		    अ -> 0 || Consonant "^" _ ;
define VowelWeakening ू -> ु || _ "^" {ओं} ,,
		      ी -> ि || _ "^" {ओं} ;
define YEpenthesis {ओं} -> {यों} || ि "^" _ ;

! Postpositions
read lexc postpositions.lexc
define Postposition

define NasalUnify ँ (->) ं ,,
       		  ं (->) ँ ;
define Cleanup "^" -> 0;

! Irregulars
read lexc irverb.lexc
define IrVerb

! Adverbs
read lexc adverbs.lexc
define Adverb

! Compose everything
regex (((Adjective .o. AdjADeletion) |
      (Noun .o. NounADeletion .o. NounIDeletion .o. VowelWeakening .o. YEpenthesis) |
      Pronoun |
      (IrVerb .P. (Verb .o. VerbVowelWeakening .o. VerbYEpenthesis)) |
      Postposition |
      Adverb)
      .o. VowelToMatra
      .o. NasalUnify
      .o. Cleanup);
